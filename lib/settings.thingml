import "datatypes.thingml" from stl

datatype PrimitiveString<0>
	@java_type "String";

thing fragment SettingsMsgs {
	message Settings(
		controlPort : Long,
		observationPort : Long,
		simulationIP : PrimitiveString,
		runTime : Long,
		tooClose : Long,
		tooFar : Long,
		sampleRate : Long
	)
}

thing fragment SettingsClient includes SettingsMsgs {
	required port Loaded {
		receives Settings
	}
}

thing SettingsParser includes SettingsMsgs
@java_import `
	import java.io.FileReader;
	import java.io.BufferedReader;
	import java.io.IOException;
`
{
	property ConfigPath : String = "Challenge problem/Settings/config.txt"
	
	provided port Loaded {
		sends Settings
	}
	
	statechart Loader init LoadConfiguration {
		state LoadConfiguration {
			property controlPort : Long= 0
			property observationPort : Long = 0
			property simulationIP : PrimitiveString = ""
			property runTime : Long = 0
			property tooClose : Long = 0
			property tooFar : Long = 0
			property sampleRate : Long = 0
			
			on entry do
				`
					try (BufferedReader br = new BufferedReader(new FileReader(`&ConfigPath&`))) {
						for (String line = br.readLine(); line != null; line = br.readLine()) {
							String[] option = line.split("=", 2);
							switch (option[0]) {
								case "controlPort":
									` controlPort = `Integer.parseInt(option[1])` `
									break;
								case "observationPort":
									` observationPort = `Integer.parseInt(option[1])` `
									break;
								case "simulationIP":
									` simulationIP = `option[1]` `
									break;
								case "runTime":
									` runTime = `Integer.parseInt(option[1])` `
									break;
								case "tooClose":
									` tooClose = `Integer.parseInt(option[1])` `
									break;
								case "tooFar":
									` tooFar = `Integer.parseInt(option[1])` `
									break;
								case "sampleRate":
									` sampleRate = `Integer.parseInt(option[1])` `
									break;
							}
						}
						` Loaded!Settings(controlPort, observationPort, simulationIP, runTime, tooClose, tooFar, sampleRate) `
					} catch (IOException e) {
						System.err.println("Couldn't read config file: "+`&ConfigPath&`);
						System.exit(1);
					}
				`
			end
		}
	}
}